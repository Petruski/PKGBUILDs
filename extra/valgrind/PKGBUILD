# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hold for toolchain update, only build for v7/v8
#  - patch from Fedora for AArch64

buildarch=12
noautobuild=1

pkgname=valgrind
pkgver=3.13.0
pkgrel=4
pkgdesc='A tool to help find memory-management problems in programs'
arch=(x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc=2.26' perl)
makedepends=(gdb openmpi)
optdepends=('openmpi: MPI support')
options=(!emptydirs)
source=("https://ftp.gwdg.de/pub/linux/sources.redhat.com/$pkgname/$pkgname-$pkgver.tar.bz2"
        'valgrind-3.13.0-arm64-hwcap.patch')
md5sums=('817dd08f1e8a66336b9ff206400a5369'
         'b7c24e58fa058578c9bf6fef4ccdd47d')
sha512sums=('34e1013cd3815d30a459b86220e871bb0a6209cc9e87af968f347083693779f022e986f211bdf1a5184ad7370cde12ff2cfca8099967ff94732970bd04a97009'
            '5af853399c6abdf8016fd1c2be854b3952b7d7e9ca16c870f84eed72e606e639d5f64de32e60105899f5201b53156380ae460111e05209e843301492742c9bfd')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../valgrind-3.13.0-arm64-hwcap.patch
}

build() {
  # valgrind does not like stack protector flags
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CXXFLAGS=${CXXFLAGS/-fstack-protector-strong/}

  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
