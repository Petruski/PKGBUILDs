# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Emanuel Couto <emanuel dot amaral dot couto at gmail dot com>
# Contributor: Richard Jackson <rdjack21 at gmail dot com>
# Contributor: Tinx <arch at tinx dot eu>
# Contributor: Jens Kapitza <j dot kapitza at schwarze-allianz dot de>
# Contributor: Olli <olli at coderkun dot de>
# Contributor: Ronny Lorenz <ronny at tbi dot univie dot ac dot at>

# Demos available in `apps/samples`
# To build them: `ant -Dplatforms.JDK_1.8.home=/usr/lib/jvm/default jar`

buildarch=4

pkgbase=java-openjfx
pkgname=('java-openjfx' 'java-openjfx-doc' 'java-openjfx-src')
_java_ver=8
_jdk_update=162
_jdk_build=12
_hgtag=${_java_ver}u${_jdk_update}-b${_jdk_build}
pkgver=${_java_ver}.u${_jdk_update}
pkgrel=1
pkgdesc='Java OpenJFX 8 client application platform (open-source implementation of JavaFX)'
arch=('i686' 'x86_64' 'armv7h')
url='https://wiki.openjdk.java.net/display/OpenJFX/Main'
license=('GPL')
makedepends=('java-environment-openjdk=8' 'bison' 'gperf' 'gtk2'
             'libxtst' 'ffmpeg' 'python2' 'qt5-base' 'webkit2gtk' 'ruby' 'cmake' 'apache-ant')
source=(http://hg.openjdk.java.net/openjfx/8u-dev/rt/archive/${_hgtag}.tar.bz2
        gradle.properties
        https://services.gradle.org/distributions/gradle-1.8-bin.zip
        01-skip-verify-java.patch
        02-use-system-libraries.patch
        03-buildflags.patch
        05-set-antlr-timeout.patch
        06-disable-architecture-verification.patch
        07-disable-assembler-on-unsupported-archs.patch
        08-disable-sse2.patch
        09-fix-conflicting-uchar32-typedef.patch
        10-javadoc-locale.patch
        11-icu-dynamic-linking.patch
        fix-arm64-build.patch
        fix-arm32-build.patch
        15-fix-javadoc-refererences.diff
        16-reproducible-build-timestamp.patch
        17-gcc-compatibility.patch
        19-disable-webkit-sampling-profiler.patch
        21-gcc7-compatibility.patch
        22-JDK-8185792-accented-characters-support.patch
        23-webcore-linker-flags.patch
        24-disable-buildSrc-tests.patch
        25-webkit-debug-level.patch
        26-disable-webkit-jit-for-armv4.patch)

sha256sums=('81e32986bb4df1b5924520bd26b107041800c452aa3b054d82f982439cc9609f'
            '1d09385ac23d755aec079954247365de3875507641f5ecd7bd3511ebf3fa9e3c'
            'a342bbfa15fd18e2482287da4959588f45a41b60910970a16e6d97959aea5703'
            'd58fd16ec1f682998fee4dcaf182e286896ec9d9d30328c7cec5acea61698f28'
            'be26d05cd315ce7c9cff395bad16a78f6391808384f1a59756079395f24a2670'
            '6080df3e4a091d07d9ee60cffa3df6e0334ddbf3751b4defb3a88bad65295429'
            '1132cac7835074395b467e49f15e10ac8e9c5ac2512c18f58cf6089b33b766ea'
            'b6122749f208772c05bf282f371e86f4b6c474998b37cdbcdd466944b52d8aff'
            '0dee35ef6bf51237e34a504336e30e592acae740261d90147a8bed76249b08e0'
            '8b6dd939292178743a3e99bfcb5b3a66557de86bf744455fd15fca4be26621b7'
            'fba880e7b68092bc3f16c793966f4319d62622bcc036e64292b6885507663252'
            '5c327b2e5554948b3db3764fc130943f8ad4c56cd8a2013f7df222456811214a'
            '1341edceae68a020ba1c6f96c063164c6230e23386d846c0631d4eb56ede4cd0'
            'fc8ed01b14be88269a6a637b4f134045129289c3cfced8b2e18b9989b6ca40a5'
            '02fd5050ee5ee70b84ad804a1321ece2468bc136aff0f1dbe3a8308c22371ff8'
            '661aeec15b9fdcb9eeab6d1d901ee98572ee76db8df301b986cbbabd7dda6420'
            '58585786e43ff376ea0aa132419f3cfc4e9620410852a74d8e38d7dad23172ea'
            '864967467efeaffdabe1e60b7cfd0a27ce93be55ef45ef9993790219ad164554'
            'd742273b4f426b2d1c5c590b4fdd8065609e7b90aa351924fe19c9780b70c8ec'
            'e0fd92d8e5d634f3cb1f3f83ad097077285832cea91894c2de6d449458852c18'
            '629c388ecac64730257a66f671f8973e1c8a627fc2046130207668dc746b1a46'
            'b3be297160d212c8851c90f7009712412e75d59966bdf9d0ad662d401aa5b354'
            '144c1e47c3fb06edf736f4e41f2269351ce0a2ea7d99fcdcc3a54be1305bc610'
            '412282d7c2eebc09124c0d745d9b769403248832d408f57fe9588eacc72c8410'
            '1fba2f17eeb53dfd294999ce5fab878b207aeefa266a4787d09c12bf7962a95f')

_openjdk8dir="/usr/lib/jvm/java-8-openjdk"

case $CARCH in
  'i686') _CARCH='i386' ;;
  'x86_64')_CARCH='amd64' ;;
  arm*    )_CARCH='arm' ;;
esac

prepare() {
  cd "rt-${_hgtag}"
  for p in \
    01-skip-verify-java.patch \
    02-use-system-libraries.patch \
    03-buildflags.patch\
    05-set-antlr-timeout.patch\
    06-disable-architecture-verification.patch\
    07-disable-assembler-on-unsupported-archs.patch\
    08-disable-sse2.patch\
    09-fix-conflicting-uchar32-typedef.patch\
    10-javadoc-locale.patch\
    11-icu-dynamic-linking.patch\
    fix-arm64-build.patch\
    fix-arm32-build.patch\
    15-fix-javadoc-refererences.diff\
    17-gcc-compatibility.patch\
    19-disable-webkit-sampling-profiler.patch\
    21-gcc7-compatibility.patch\
    22-JDK-8185792-accented-characters-support.patch\
    23-webcore-linker-flags.patch\
    24-disable-buildSrc-tests.patch\
    25-webkit-debug-level.patch\
    26-disable-webkit-jit-for-armv4.patch; do
    patch -p1 < "${srcdir}/${p}"
  done
}

build() {
  cd "rt-${_hgtag}"

  ln -sf "${srcdir}/gradle.properties" .
  export GRADLE_USER_HOME="${srcdir}/gradle_home"
  mkdir -p ${GRADLE_USER_HOME}

  # work around a compilation issue with Gradle 2.x
  mkdir -p modules/web/build/linux/Release/WebCore/generated/java/com/sun/webkit/dom/
  mkdir -p modules/web/build/linux/Release/WebCore/generated/java/com/sun/webkit/perf/
  cp modules/web/src/main/java/com/sun/webkit/Disposer*            modules/web/build/linux/Release/WebCore/generated/java/com/sun/webkit/
  cp modules/web/src/main/java/com/sun/webkit/Invoker.java         modules/web/build/linux/Release/WebCore/generated/java/com/sun/webkit/
  cp modules/web/src/main/java/com/sun/webkit/dom/JSObject.java    modules/web/build/linux/Release/WebCore/generated/java/com/sun/webkit/dom/
  cp modules/web/src/main/java/com/sun/webkit/perf/PerfLogger.java modules/web/build/linux/Release/WebCore/generated/java/com/sun/webkit/perf/

  # Copy the ICU and sqlite libraries required by JavaScriptCore
  mkdir -p modules/web/build/linux/import/lib/
  cp /usr/lib/libicui18n.so modules/web/build/linux/import/lib/libicui18n.a
  cp /usr/lib/libicuuc.so   modules/web/build/linux/import/lib/libicuuc.a
  cp /usr/lib/libicudata.so modules/web/build/linux/import/lib/libicudata.a
  cp /usr/lib/libsqlite3.so modules/web/build/linux/import/lib/libsqlite3.a



  "${srcdir}"/gradle-1.8/bin/gradle
}

package_java-openjfx() {
  pkgdesc='Java OpenJFX 8 client application platform (open-source implementation of JavaFX)'
  depends=('java-runtime-openjdk=8' 'gstreamer' 'libxtst' 'webkit2gtk' 'ffmpeg' 'qt5-base'  'swt' 'icu' 'sqlite')
  conflicts=('openjfx')
  replaces=('openjfx')

  local _builddir="${srcdir}/rt-${_hgtag}/build"
  local _sdkdir="${_builddir}/sdk"

  install -d "${pkgdir}${_openjdk8dir}/jre/lib/${_CARCH}"
  install -m755 "${_sdkdir}/rt/lib/${_CARCH}"/*.* "${pkgdir}${_openjdk8dir}/jre/lib/${_CARCH}"

  install -d "${pkgdir}${_openjdk8dir}/jre/lib/ext"
  install -m644 "${_sdkdir}/rt/lib/ext"/*.* "${pkgdir}${_openjdk8dir}/jre/lib/ext"
  install -m644 "${_sdkdir}/rt/lib"/*.* "${pkgdir}${_openjdk8dir}/jre/lib"

  install -d "${pkgdir}${_openjdk8dir}/lib"
  install -m644 "${_sdkdir}/lib"/*.* "${pkgdir}${_openjdk8dir}/lib"

  install -d "${pkgdir}${_openjdk8dir}/bin"
  install -m755 "${_sdkdir}/bin"/* "${pkgdir}${_openjdk8dir}/bin"

  install -m644 -D "${_sdkdir}/man/man1/javapackager.1" "${pkgdir}/usr/share/man/man1/javapackager.1"
}

package_java-openjfx-doc() {
  pkgdesc='Java OpenJFX 8 client application platform (open-source implementation of JavaFX) - documentation'
  conflicts=('openjfx-doc')
  replaces=('openjfx-doc')

  local _builddir="${srcdir}/rt-${_hgtag}/build"
  local _sdkdir="${_builddir}/sdk"
  local docdir="/usr/share/doc"

  install -d "${pkgdir}${docdir}/openjfx"
  cp -dr --no-preserve=ownership "${_builddir}/javadoc"/* "${pkgdir}${docdir}/openjfx"
}

package_java-openjfx-src() {
  pkgdesc='Java OpenJFX 8 client application platform (open-source implementation of JavaFX) - sources'
  conflicts=('openjfx-src')
  replaces=('openjfx-src')

  local _builddir="${srcdir}/rt-${_hgtag}/build"
  local _sdkdir="${_builddir}/sdk"

  install -d "${pkgdir}${_openjdk8dir}"
  install -m644 "${_builddir}/javafx-src.zip" "${pkgdir}${_openjdk8dir}"
}
