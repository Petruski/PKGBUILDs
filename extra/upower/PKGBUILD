# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.5+2+g502c63b
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd' 'libusb' 'libimobiledevice' 'libgudev')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'python2' 'git' 'gtk-doc')
backup=('etc/UPower/UPower.conf')
_commit=502c63b54944064dfd5f5cc4463bfb3d457a95c0
source=(git://anongit.freedesktop.org/upower#commit=$_commit
        'handle_at_sign_in_device_path.patch')

sha256sums=('SKIP'
            '22e1e42239f2c04216bdc298ea054efbc9176fd3bf83f820df6a0c63e61182b7')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/UPOWER_//' -e 's/_/\./g' -e 's/-/+/g'
}

build() {
  cd $pkgname

  patch -p1 < "${srcdir}/handle_at_sign_in_device_path.patch"

  NOCONFIGURE=1 ./autogen.sh

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
