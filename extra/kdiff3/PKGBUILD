# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to explicitly link v5/v6 with libatomic

pkgname=kdiff3
pkgver=1.7.90.git20190329
pkgrel=1
_commit=0caf3edb739cfcf5a57df305c6d6dec37dbc8bff
pkgdesc="A KDE file comparator/merge tool"
arch=(x86_64)
url="https://kdiff3.sourceforge.net/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools git)
source=(git://git.kde.org/kdiff3#commit=$_commit
        0001-link-atomic.patch)
sha256sums=('SKIP'
            '414fd602932d4572eb564da71c8adde822dfcc96727693c266195580a04d9809')

prepare() {
  mkdir -p build
  if [[ $CARCH == "armv6h" || $CARCH == "arm" ]]; then
    cd $pkgname
    patch -p1 -i ../0001-link-atomic.patch
  fi
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
