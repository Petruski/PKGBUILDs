# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on numactl

pkgname=irqbalance
pkgver=1.5.0
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('x86_64')
url="https://github.com/Irqbalance/irqbalance"
license=('GPL')
backup=('etc/irqbalance.env')
depends=('glib2' 'libcap-ng' 'systemd-libs')
makedepends=('systemd')
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        config.patch)
sha1sums=('bb8c786b1aae73679016ae0bbbb7c7dbc488be64'
          '32e5650f37c64531e69f477959b0c3598acc2825')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../config.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --with-systemd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"

  install -D -m644 misc/irqbalance.service "$pkgdir"/usr/lib/systemd/system/irqbalance.service
  install -D -m644 misc/irqbalance.env "$pkgdir"/etc/irqbalance.env

  # Disable udev rule until we clarify it FS#56785
  # install -D -m644 misc/90-irqbalance.rules "$pkgdir"/usr/lib/udev/rules.d/90-irqbalance.rules
}
