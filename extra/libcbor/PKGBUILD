# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Moritz Kiefer <moritz.kiefer@purelyfunctional.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove -flto from CMakeLists
#  - upstream patch to fix FTBFS

pkgname=libcbor
pkgver=0.6.1
pkgrel=1
pkgdesc='C library for parsing and generating CBOR, the general-purpose schema-less binary data format'
url='https://github.com/PJK/libcbor'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'cmocka')
provides=('libcbor.so')
source=(https://github.com/PJK/libcbor/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/PJK/libcbor/commit/cdeef0a4ac3e281feca741be79ab855da4e9f855.patch)
sha256sums=('2f805f5fa2790c4fdd16046287f5814703229547da2b83009dd32357623aa182'
            '69e3b8238a747732885876d130da2c9cf181a096cf257915b04995dc1e9e9e4d')
b2sums=('9a2415c1dde7ed611075c3b03ea1d8ab63bcb00dfc0cae3d7a3bb225b6fbee2a8f576b1f0f97eb6f0cf14c1a6e9df1c77f69320bac041287b0dec37c073f8b85'
        '3ad723bb1b91c1aeb4d9fb69e5f48c4b2fc13ed3c13fe14337405638ed477e579a51616119a4529d12b61953c1fed4db60c36475c725d2f7167754c0705136b2')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../cdeef0a4ac3e281feca741be79ab855da4e9f855.patch
  sed -i 's/-flto//g' CMakeLists.txt
}

build() {
  mkdir -p ${pkgname}-${pkgver}/build
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DWITH_TESTS=1
  make cbor cbor_shared VERBOSE=1
  make -C test VERBOSE=1
}

check() {
  cd ${pkgname}-${pkgver}/build
  make -C test test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
