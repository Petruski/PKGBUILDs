# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop julia make/optdepends, doesn't build on ARM

pkgname=cantor
pkgver=18.08.0
pkgrel=5
pkgdesc="KDE Frontend to Mathematical Software"
url="https://kde.org/applications/education/cantor/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(analitza libspectre kpty ktexteditor knewstuff libqalculate hicolor-icon-theme)
makedepends=(extra-cmake-modules python python2 kdoctools luajit r)
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'luajit: LUA backend'
            'python: Python 3 backend'
	    'python2: Python 2 backend'
            'sagemath: SageMath backend')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        cantor-julia.patch::"https://cgit.kde.org/cantor.git/patch/?id=90b001d1" cantor-julia-0.7.patch sage-8.3.patch cantor-julia-1.0.patch)
sha256sums=('be6167ec88504091e9fa9914e0bd1fbb13637fb2b2b228c9274225db6dc85823'
            'SKIP'
            '366e1a734a968c4a933e91342308e4d18108c1bc023572848747ca6a2ec656f7'
            'cf031eb1891a97f964c9147a4ff4d7e69f3821eee8fb5dab0693c6483dacd4ee'
            '78c90097749e53433641b3acfd637b2aaa12f04c03a304b71c2cc5a5672cb4de'
            '81b6752473657318d91e748c6c1799cdd285037fa1bdfad29d401cacf9d98f7b')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../cantor-julia.patch # Fix julia backend build
  patch -p1 -i ../cantor-julia-0.7.patch # support julia>=0.7
  patch -p1 -i ../cantor-julia-1.0.patch # support julia>=1.0
  patch -p1 -i ../sage-8.3.patch # fix sagemath backend with 8.3
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
