# Maintainer: RaumZeit <raumzeit@gmx.net>

buildarch=4
pkgbase=gpu-nvidia-tegra-k1
_nv_full_release=r21_Release_v5.0
_nv_version=21.5
_nv_release=0
pkgver="${_nv_version}.${_nv_release}"
pkgrel=5
pkgdesc="NVIDIA proprietary Tegra K1 GPU drivers for Tegra K1 powered SoC"

# filesystem prefix for the include files and license
_install_prefix=/usr

url="https://developer.nvidia.com/embedded/linux-tegra"

arch=('armv7h')
license=('proprietary')

source=("http://developer.download.nvidia.com/embedded/L4T/${_nv_full_release}/Tegra124_Linux_R${pkgver}_armhf.tbz2"
        51-nvrm.rules
        tegra.conf)
sha256sums=('a639e3e37e17ef11ea5754d7b703a133febdd6a5d3dd2e5b3924288930a45cca'
            'bdff89e517cd8cbba5f9e9d960bc79d7c2740bf563ff6aaab789a13a547f08fe'
            '05cf40f5722a33a2bf13631eadb6c49be81f8f70e0bf85db896c71d1a6f519e1')

conflicts=( 'X-ABI-VIDEODRV_VERSION<19'
            'X-ABI-VIDEODRV_VERSION>=20'
            'X-ABI-XINPUT_VERSION>=22'
            'X-ABI-XINPUT_VERSION<21')

prepare() {
  cd "${srcdir}"/Linux_for_Tegra/nv_tegra/
  tar xjf nvidia_drivers.tbz2
}

package_gpu-nvidia-tegra-k1-nvrm() {
  pkgdesc="NVIDIA binary nvrm daemon and libraries for Tegra K1"
  depends=('linux-tegra-k1')
  provides=('gpu-nvidia-tegra-k1="${pkgver}"')
  conflicts=('gpu-nvidia-tegra-k1')

  cd "${srcdir}/Linux_for_Tegra/nv_tegra"

  mkdir -p "${pkgdir}${_install_prefix}/lib/tegra"

  # copy other tegra specific libs
  for lib in  libnvapputil.so \
              libnvavp.so \
              libnvdc.so \
              libnvddk_2d_v2.so \
              libnvddk_vic.so \
              libnvmm.so \
              libnvmm_camera_v3.so \
              libnvmm_contentpipe.so \
              libnvmm_parser.so \
              libnvmm_utils.so \
              libnvmmlite.so \
              libnvmmlite_audio.so \
              libnvmmlite_image.so \
              libnvmmlite_utils.so \
              libnvmmlite_video.so \
              libnvodm_imager.so \
              libnvodm_query.so \
              libnvos.so \
              libnvparser.so \
              libnvrm.so \
              libnvrm_graphics.so \
              libnvsm.so \
              libnvtestio.so \
              libnvtestresults.so \
              libnvtnr.so \
              libnvtvmr.so \
              libnvwinsys.so \
              libtegrav4l2.so
  do
    cp usr/lib/arm-linux-gnueabihf/tegra/${lib} "${pkgdir}${_install_prefix}/lib/tegra"
  done

  for lib in  libnvidia-eglcore.so \
              libnvidia-glcore.so \
              libnvidia-glsi.so \
              libnvidia-rmapi-tegra.so \
              libnvidia-tls.so
  do
    cp usr/lib/arm-linux-gnueabihf/tegra/"${lib}.${_nv_version}" "${pkgdir}${_install_prefix}/lib/tegra"
    ln -s "${_install_prefix}/lib/tegra/${lib}.${_nv_version}" "${pkgdir}${_install_prefix}/lib/tegra/${lib}"
  done

  # copy license
  mkdir -p "${pkgdir}${_install_prefix}/share/licenses/nvidia-tegra-k1"
  cp LICENSE "$pkgdir${_install_prefix}/share/licenses/nvidia-tegra-k1/"

  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "${_install_prefix}/lib/tegra" > "${pkgdir}/etc/ld.so.conf.d/$pkgbase.conf"

  # copy udev rules
  mkdir -p "${pkgdir}/usr/lib/udev/rules.d/"
  install -D -m0644 "${srcdir}"/51-nvrm.rules "${pkgdir}/usr/lib/udev/rules.d/51-nvrm.rules"

}

package_gpu-nvidia-tegra-k1-x11() {
  pkgdesc="NVIDIA binary OpenGL|ES libraries and X11 drivers for Tegra K1"
  depends=('gpu-nvidia-tegra-k1="${pkgver}"' 'X-ABI-VIDEODRV_VERSION=19')
  provides=('opengl-driver' 'tegra-k1-libgl')
  cd "${srcdir}/Linux_for_Tegra/nv_tegra"

  # prepare tegra libgl stuff
  mkdir -p "${pkgdir}${_install_prefix}/lib/tegra"
  mkdir -p "${pkgdir}${_install_prefix}/lib/tegra-egl"

  # first libGL
  cp -r usr/lib/arm-linux-gnueabihf/tegra/libGL.so.1 "${pkgdir}${_install_prefix}/lib/tegra/libGL.so.1.2.0"
  ln -s "${_install_prefix}/lib/tegra/libGL.so.1.2.0" "${pkgdir}${_install_prefix}/lib/tegra/libGL.so.1"
  ln -s "${_install_prefix}/lib/tegra/libGL.so.1.2.0" "${pkgdir}${_install_prefix}/lib/tegra/libGL.so"

  # second libEGL, and libGLES
  cp -r usr/lib/arm-linux-gnueabihf/tegra-egl/libEGL.so.1 "${pkgdir}${_install_prefix}/lib/tegra-egl/libEGL.so.1.0.0"
  ln -s "${_install_prefix}/libtegra-egl/libEGL.so.1.0.0" "${pkgdir}${_install_prefix}/lib/tegra-egl/libEGL.so.1"
  ln -s "${_install_prefix}/lib/tegra-egl/libEGL.so.1.0.0" "${pkgdir}${_install_prefix}/lib/tegra-egl/libEGL.so"
  cp -r usr/lib/arm-linux-gnueabihf/tegra-egl/libGLESv1_CM.so.1 "${pkgdir}${_install_prefix}/lib/tegra-egl/libGLESv1_CM.so.1.0.0"
  ln -s "${_install_prefix}/lib/tegra-egl/libGLESv1_CM.so.1.0.0" "${pkgdir}${_install_prefix}/lib/tegra-egl/libGLESv1_CM.so.1"
  ln -s "${_install_prefix}/lib/tegra-egl/libGLESv1_CM.so.1.0.0" "${pkgdir}${_install_prefix}/lib/tegra-egl/libGLESv1_CM.so"
  cp -r usr/lib/arm-linux-gnueabihf/tegra-egl/libGLESv2.so.2 "${pkgdir}${_install_prefix}/lib/tegra-egl/libGLESv2.so.2.0.0"
  ln -s "${_install_prefix}/lib/tegra-egl/libGLESv2.so.2.0.0" "${pkgdir}${_install_prefix}/lib/tegra-egl/libGLESv2.so.2"
  ln -s "${_install_prefix}/lib/tegra-egl/libGLESv2.so.2.0.0" "${pkgdir}${_install_prefix}/lib/tegra-egl/libGLESv2.so"

  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "${_install_prefix}/lib/tegra-egl" > "${pkgdir}/etc/ld.so.conf.d/${pkgbase}-egl.conf"

  # copy X11 drivers
  mkdir -p "${pkgdir}/usr/lib/xorg/modules/drivers"
  mkdir -p "${pkgdir}/usr/lib/xorg/modules/extensions"
  cp usr/lib/arm-linux-gnueabihf/tegra/libglx.so "${pkgdir}/${_install_prefix}/lib/tegra"
  cp -r usr/lib/xorg/modules/drivers/nvidia_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
  ln -s ${_install_prefix}/lib/tegra/libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  # install tegra X11 config
  mkdir -p "${pkgdir}/usr/share/X11/xorg.conf.d/"
  cp "${srcdir}/tegra.conf" "${pkgdir}/usr/share/X11/xorg.conf.d/"
}

package_gpu-nvidia-tegra-k1-libcuda() {
  depends=('gpu-nvidia-tegra-k1="${pkgver}"')
  provides=('libcuda')
  conflicts=('libcuda')

  cd "${srcdir}/Linux_for_Tegra/nv_tegra"

  mkdir -p "${pkgdir}${_install_prefix}/lib/tegra"

  cp usr/lib/arm-linux-gnueabihf/tegra/libcuda.so.1.1 "${pkgdir}/${_install_prefix}/lib/tegra/libcuda.so.1.1.0"
  ln -s "${_install_prefix}/lib/tegra/libcuda.so.1.1.0" "${pkgdir}${_install_prefix}/lib/tegra/libcuda.so.1.1"
  ln -s "${_install_prefix}/lib/tegra/libcuda.so.1.1.0" "${pkgdir}${_install_prefix}/lib/tegra/libcuda.so.1"
  ln -s "${_install_prefix}/lib/tegra/libcuda.so.1.1.0" "${pkgdir}${_install_prefix}/lib/tegra/libcuda.so"

}

package_gpu-nvidia-tegra-k1-openmax() {
  pkgdesc="OpenMAX binary libraries for NVIDIA Tegra K1"
  depends=('gpu-nvidia-tegra-k1="${pkgver}"')

  cd "${srcdir}/Linux_for_Tegra/nv_tegra"

  mkdir -p "${pkgdir}${_install_prefix}/lib/tegra"

  for lib in libnvomx.so libnvomxilclient.so
  do
    cp usr/lib/arm-linux-gnueabihf/tegra/${lib} "${pkgdir}/${_install_prefix}/lib/tegra/"
  done

  ln -s "${_install_prefix}/lib/tegra/libnvomx.so" "${pkgdir}${_install_prefix}/lib/tegra/libOmxCore.so"

}

package_gpu-nvidia-tegra-k1-openmax-codecs() {
  depends=('gpu-nvidia-tegra-k1="${pkgver}"')
  pkgdesc="OpenMAX binary libraries for NVIDIA Tegra K1"

  cd "${srcdir}/Linux_for_Tegra/nv_tegra"

  mkdir -p "${pkgdir}/usr/lib/firmware/tegra12x"

  for f in nvhost_msenc031.fw nvhost_tsec.fw vic03_ucode.bin
  do
    cp lib/firmware/tegra12x/${f} "${pkgdir}/usr/lib/firmware/tegra12x/"
  done

}

pkgname=("${pkgbase}-nvrm" "${pkgbase}-x11" "${pkgbase}-libcuda" "${pkgbase}-openmax-codecs" "${pkgbase}-openmax")



