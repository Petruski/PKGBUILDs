# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with NOOPT=true

_name=Carla
pkgname=carla
pkgver=2.1
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11' 'python-pyqt5' 'python-pyliblo'
'python-rdflib' 'qt5-base' 'qt5-svg')
# libpulse is linked against in carla/libcarla_standalone2.so
# liblo is linked against in carla/libcarla_standalone2.so,
# carla/libcarla_native-plugin.so and the lv2 and vst plugins
makedepends=('alsa-lib' 'file' 'fluidsynth' 'glib2' 'gtk2' 'gtk3' 'libsndfile'
'liblo' 'libpulse' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('b265e3150e12b42095e28ac30f72e211e96a769619a5ec56d574be8a17fb6ca282a504bb9759de312066202106b6e5234e006689b90edb1cc54f3e6919384509')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd $pkgname-$pkgver
  # fixing file (libmagic.so) detection
  sed -e 's/\\#include/#include/' -i source/Makefile.mk
}

build() {
  cd "${pkgname}-${pkgver}"
  make NOOPT=true features
  make NOOPT=true DEFAULT_QT=5
}

package() {
  depends+=('libasound.so' 'libfluidsynth.so' 'libgdk-3.so'
  'libgdk-x11-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so' 'libgtk-3.so'
  'libgtk-x11-2.0.so' 'liblo.so' 'libmagic.so' 'libpulse.so'
  'libpulse-simple.so' 'libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make NOOPT=true DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
