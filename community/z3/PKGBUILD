# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix ARM FTBFS

pkgbase=z3
pkgname=('z3' 'python-z3')
pkgver=4.8.4
pkgrel=2
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=('x86_64')
license=('MIT')
 # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib'
makedepends=('cmake' 'python' 'python-setuptools' 'gcc-libs' 'gmp')
source=(https://github.com/Z3Prover/z3/archive/${pkgbase}-${pkgver}.tar.gz
        z3-no-sse.patch)
sha512sums=('4660ba6ab33a6345b2e8396c332d4afcfc73eda66ceb2595a39f152df4d62a9ea0f349b0f9212389ba84ecba6bdae6ad9b62b376ba44dc4d9c74f80d7a818bf4'
            '90dc75045745c59456327a92b6b6b90547f7a6f130720bb683cc5f6c3ea46fcef271e74d2d319c05515af48061414d2eca3c7d6b4e36451547e07f30e8ece132')

prepare() {
  cd ${pkgbase}-${pkgbase}-${pkgver}
  mkdir -p build
  patch -p0 -i ../z3-no-sse.patch
}

build() {
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DUSE_LIB_GMP=1
  make
}

package_z3() {
  depends=('gcc-libs' 'gmp')
  provides=('libz3' 'libz3.so')
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}"/usr/lib/python*
}

package_python-z3() {
  depends=('python' 'libz3')
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${pkgdir}"
  rm -r usr/include usr/bin usr/lib/cmake usr/lib/lib*
  python -m compileall .
  python -O -m compileall .
}

# vim: ts=2 sw=2 et:
