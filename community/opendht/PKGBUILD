# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set -DOPENDHT_LTO=OFF
#  - explicitly link v5/v6 with libatomic

pkgname=opendht
pkgver=1.7.0rc4
pkgrel=1
epoch=1
pkgdesc="A C++11 implementation of the Kademlia DHT (Distributed Hash Table)"
arch=('x86_64')
depends=('gnutls' 'nettle' 'readline' 'argon2' 'jsoncpp' 'restbed')
makedepends=('msgpack-c' 'cmake' 'cython' 'systemd')
optdepends=('python: to use the Python bindings')
url="https://github.com/savoirfairelinux/opendht"
license=('GPL3')
source=("https://github.com/savoirfairelinux/opendht/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('17dbfcd16a699047bdd03586f4329f408c56c7bede42d28e25344c777fa0058b')
sha512sums=('38cd7923e4530ce86d62cba452ff9d09c4406a2b1e4559bc309b62ba12c1810bdf8029a08fd6b347634b03cccd9ae4bf776dca72c5562b0a447ce57da20907ee')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && echo "target_link_libraries(opendht PRIVATE atomic)" >> CMakeLists.txt

  msg2 'Building...'
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_LTO=OFF \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg2 'Installing...'
  cd build
  make DESTDIR="$pkgdir" install
  cd ..

  msg2 'Installing documentation...'
  install -D -m644 README.md "${pkgdir}/usr/share/doc/opendht/README.md"
}

# vim:set ts=2 sw=2 et:
