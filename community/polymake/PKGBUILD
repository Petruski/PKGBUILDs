# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add LDFLAGS and CXXOPT to lower RAM usage
#  - make -j2 due to RAM constraints

highmem=1

pkgname=polymake
_pkgver=3.3
_patchver=4
#pkgver=$_pkgver.r$_patchver
pkgver=$_pkgver
pkgrel=3
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(mpfr normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt perl-term-readline-gnu perl-term-readkey perl-json)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${_pkgver}-minimal.tar.bz2"
        polymake-fix-lib-loading.patch)
sha256sums=('f7867137f99d5e8b86035ba05b59a63c5b73a95b21b88059fd2439216c128792'
            '491cf7ea37e58e9289b81feb5604473b960e3688969a8d7854378fd6f9deb93a')

prepare() {
  cd polymake-$_pkgver
  patch -p1 -i ../polymake-fix-lib-loading.patch # Fix loading shared library from external programs
}

build() {
  cd polymake-$_pkgver
  LDFLAGS+=" -Wl,--no-keep-memory -Wl,--reduce-memory-overheads"
  ./configure --prefix=/usr CXXOPT="-O1"
  make -j2
}

package() {
  cd polymake-$_pkgver
  make DESTDIR="$pkgdir" install
}
