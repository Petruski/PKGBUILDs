# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_TESTS=OFF

pkgname=curaengine
pkgver=4.5.0
pkgrel=1
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus' 'rapidjson' 'polyclipping')
checkdepends=('cppunit')
makedepends=('cmake' 'git' 'gmock' 'gtest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('481f6b839fe7d4bad14d9820337574d31a928e258ede359c608075f1cc7771e3eb649d5d0cff5f1edbc1e2c1802ee99e1ca2eb36a34799331279cbc0c3294636')


build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=OFF \
     -DUSE_SYSTEM_LIBS=ON \
     -DCMAKE_BUILD_TYPE=Release

  make
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
