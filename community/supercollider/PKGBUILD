# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.11.0
pkgrel=4
pkgdesc="Environment and programming language for real time audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc'  'libx11' 'qt5-base' 'qt5-svg' 'qt5-webengine'
'qt5-websockets')
makedepends=('abletonlink' 'alsa-lib' 'avahi' 'boost' 'cmake' 'emacs' 'fftw'
'libsndfile' 'qt5-tools' 'readline' 'systemd-libs' 'yaml-cpp')
checkdepends=('xorg-server-xvfb')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'sc3-plugins: additional extension plugins for scsynth')
provides=("libscsynth.so" "sclang=${pkgver}" "scsynth=${pkgver}")
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source.tar.bz2"
        "${pkgname}-3.11.0-use_system_link.patch")
install="${pkgname}.install"
sha512sums=('64fef4d751436337baf40ced068a496d3e9221f751dcde7bfc2857a79c094764dff814d1c0cf519380053ddcfdb2c3f2b8eb787d3cff03e522c19fd95951b1da'
            '1a9977f7b1a1a90ea3b4163ef6f6f963325e8d87b6ca16fecaa5a1816323d6cb4ab7eb750957d58be34a99fd3ed08ed7b7a92ac5352f31c894a8eb002f0aa71c')


prepare() {
  mv -v "${_name}-${pkgver}-Source" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # devendoring Ableton Link:
  # https://github.com/supercollider/supercollider/issues/4818
  # https://github.com/supercollider/supercollider/pull/4836
  patch -Np1 -i "../${pkgname}-3.11.0-use_system_link.patch"
  # removing macOS hidden files (due to release tarball issues):
  # https://github.com/supercollider/supercollider/issues/4545
  find . -type f -iname "*\._*" -delete
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DLIBSCSYNTH=ON \
        -DFORTIFY=ON \
        -DSYSTEM_ABLETON_LINK=ON \
        -DSYSTEM_BOOST=ON \
        -DSYSTEM_YAMLCPP=ON \
        -DSC_VIM=OFF \
        -B build \
        -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
        -DCMAKE_C_FLAGS="${CFLAGS}" \
        .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}/build"
  xvfb-run make test ARGS="-V" -C build || echo "Known failing tests: https://github.com/supercollider/supercollider/issues/3555"
}

package() {
  depends+=('libasound.so' 'libavahi-common.so' 'libavahi-client.so'
  'libboost_filesystem.so' 'libboost_program_options.so' 'libboost_regex.so'
  'libboost_thread.so' 'libfftw3f.so' 'libjack.so' 'libsndfile.so'
  'libreadline.so' 'libudev.so' 'libyaml-cpp.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,{CHANGELOG,README,README_LINUX}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
