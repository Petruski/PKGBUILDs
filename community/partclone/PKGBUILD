# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove fail-mbr from build

pkgname=partclone
pkgver=0.2.89
pkgrel=3
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=('x86_64')
url="https://partclone.org"
license=('GPL')
depends=('progsreiserfs' 'ntfs-3g' 'nilfs-utils')
source=(https://partclone.nchc.org.tw/download/stable/$pkgver/${pkgname}_$pkgver.orig.tar.gz)
md5sums=('1b6602fa623afc37a8c5252335d1bb01')

build() {
  cd $pkgname-$pkgver

# enable xfs again when it's updated to not use the deprecated ustat interface
  sed -i 's/fail-mbr//' Makefile.in
  ./configure --prefix=/usr --enable-extfs --enable-reiserfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs \
    --enable-exfat --enable-f2fs --enable-minix --enable-nilfs2 \
    --sbindir=/usr/bin

  make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
