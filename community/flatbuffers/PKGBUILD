# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix FTBFS on v5/v6/v7

pkgname=flatbuffers
pkgver=1.8.0
pkgrel=1
pkgdesc='An efficient cross platform serialization library for C++, with support for Java, C# and Go'
arch=(x86_64)
url='http://google.github.io/flatbuffers/'
license=(Apache)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::https://github.com/google/$pkgname/archive/v$pkgver.tar.gz
        https://github.com/google/flatbuffers/commit/474ba68bba935277ea132f73f601898754c3d82c.patch)
sha256sums=('c45029c0a0f1a88d416af143e34de96b3091642722aa2d8c090916c6d1498c2e'
            'adb2d4022de6cf54bf2736799ea33a1f113ac677413725cec53df29685eb0053')

prepare() {
  mkdir -p build
  patch -p1 -d $pkgname-$pkgver -i ../474ba68bba935277ea132f73f601898754c3d82c.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DFLATBUFFERS_BUILD_SHAREDLIB=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm755 flatc -t "$pkgdir"/usr/bin
}
