# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove --enable-sse from configure

pkgname=calf
pkgver=0.90.1
pkgrel=4
pkgdesc='LV2/DSSI/LADSPA plug-in suite and standalone Jack host'
arch=('x86_64')
url='https://calf-studio-gear.org/'
groups=('lv2-plugins' 'pro-audio')
license=('LGPL')
depends=('fftw' 'fluidsynth' 'gtk2')
makedepends=('lv2')
source=("https://calf-studio-gear.org/files/$pkgname-$pkgver.tar.gz"
        "${pkgname}-fluidsynth2.patch::https://github.com/calf-studio-gear/${pkgname}/pull/203/commits/bba03b6080dc198f3513b5c29fe1ba4ff9e4aa59.patch")
sha256sums=('eb3ac2e48952cf68b2d87151ce4cf58ea1fcd4433629ff0628b6e4165fe9dc7e'
            'adc6169efcb42cbdb67c117c81a88ec8a4314a8ef7744414b6bd5ea5f3fbf004')

prepare(){
  cd "$pkgname-$pkgver"
  # fluidsynth2 patch: https://github.com/calf-studio-gear/calf/issues/202
  patch -Np1 -i ../${pkgname}-fluidsynth2.patch
  # replace check for lv2core: https://github.com/calf-studio-gear/calf/issues/220
  sed -e 's/lv2core >= 6/lv2 >= 1.14/g' -i configure.ac
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-lv2 \
              --enable-experimental
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
