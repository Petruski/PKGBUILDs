# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure with --disable-sse

pkgname=audacity
pkgver=2.2.1
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="http://audacityteam.org"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk3' 'lame' 'soundtouch'
         'ffmpeg' 'vamp-plugin-sdk' 'portsmf' 'twolame' 'lv2' 'suil' 'lilv') # sbsms - needs unreleased version
makedepends=('cmake' 'python2')
source=(https://github.com/audacity/audacity/archive/Audacity-${pkgver}.zip)
sha256sums=('48dfff3d9319aa1a1a9e0e791846ca697b40079d05d652c900613415eede4db7')

build() {
  cd audacity-Audacity-${pkgver}
  WX_CONFIG=wx-config-gtk3 ./configure --prefix=/usr --with-libsamplerate --disable-sse
  make
}

package() {
  cd audacity-Audacity-${pkgver}
  make DESTDIR="${pkgdir}" install
}
