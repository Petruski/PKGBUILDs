# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix boost library path for ARM

pkgbase=qbittorrent
pkgname=(qbittorrent qbittorrent-nox)
pkgver=4.0.1
pkgrel=1
arch=(x86_64)
url="https://www.qbittorrent.org"
license=(custom GPL)
makedepends=(libtorrent-rasterbar boost qt5-tools qt5-svg)
source=(https://downloads.sourceforge.net/sourceforge/qbittorrent/$pkgname-$pkgver.tar.xz{,.asc}
        0001-fix-boost-library-path-for-ARM.patch)
sha256sums=('e30ff8b5088861467490a16dcc5988e72e2dfbd2fc3a4fd1a43941a8ca411e4e'
            'SKIP'
            'f040f1fec3a0f2fa62ffccc9a65fd9206ebdaa58ce082a0739b7d03e560bd5de')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2')

prepare() {
  cd $pkgbase-$pkgver
  patch -p1 -i ../0001-fix-boost-library-path-for-ARM.patch
}

build() {
  cd $pkgbase-$pkgver

  mkdir $pkgbase
  pushd $pkgbase
  ../configure --prefix=/usr
  make

  # Build nox variant
  popd
  mkdir "$pkgbase-nox"
  pushd "$pkgbase-nox"
  ../configure --prefix=/usr --disable-gui --enable-systemd
}

package_qbittorrent() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends=(libtorrent-rasterbar qt5-svg hicolor-icon-theme)
  optdepends=('python: needed for torrent search tab')

  cd $pkgbase-$pkgver/$pkgbase

  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgbase-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_qbittorrent-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"
  depends=(libtorrent-rasterbar qt5-base)

  cd $pkgbase-$pkgver/$pkgbase-nox

  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgbase-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
